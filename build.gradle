buildscript {

    ext {

        /////////////////////////////////////////////////////////////////////////
        // Versions

        // Tooling
        ktlintVersion = '0.8.3'

        // Libraries
        kotlinVersion = '1.1.3'

        // Testing
        jUnitVersion = '4.12'
        jUnitPlatformRunnerVersion = '1.0.0-M4'
        jUnitQuickCheckVersion = '0.8-alpha-7'
        truthVersion = '0.34'

        /////////////////////////////////////////////////////////////////////////
        // Dependencies

        // Tooling
        ktlintDep = "com.github.shyiko:ktlint:$ktlintVersion"

        // Libraries
        kotlinStdLib = "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        // Testing
        jUnit = "junit:junit:$jUnitVersion"
        jUnitPlatformRunner = "org.junit.platform:junit-platform-runner:$jUnitPlatformRunnerVersion"
        jUnitQuickCheckCore = "com.pholser:junit-quickcheck-core:$jUnitQuickCheckVersion"
        jUnitQuickCheckGenerators = "com.pholser:junit-quickcheck-generators:$jUnitQuickCheckVersion"
        truth = "com.google.truth:truth:$truthVersion"
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

subprojects {

    apply plugin: 'kotlin'

    repositories {
        jcenter()
    }

    configurations {
        ktlint
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    check.dependsOn ktlint
}
